dependencies {
	compile ("org.easyb:easyb:0.9.7") {
		exclude module: "groovy-all"
	}
}

test.doLast {
	assert reports.html.destination || reports.html.destination.mkdirs()
	ant.java(
		classname: 'org.easyb.BehaviorRunner', 
		fork: true,
		classpath: sourceSets.test.runtimeClasspath.asPath,
		output: "$reports.html.destination.absolutePath/easyb-output.txt",
		failonerror: false,
		dir: projectDir,
		resultproperty: 'easybresult'
	) {
		fileTree(dir: "${projectDir}/src/test/stories").matching { include "*" }.each { 
			arg(value: it.absolutePath) 
		}
		arg(value: "-txtstory")
		arg(value: "$reports.html.destination.absolutePath/easyb.txt")
	}
	
	if (ant.easybresult != "0") System.err.println ">> Easyb Failed ($ant.easybresult)"
}

modifyPom { pom ->
	pom.project {
		name "Geb for EasyB"
		description "Geb (pronounced \"jeb\") integration with the EasyB test framework."
	}
	
	// User provides their own easyb
	pom.dependencies.removeAll(pom.dependencies.findAll { it.groupId == "org.easyb" })
}
